DECLARE VOICES: soprano, alto, tenor, bass
DECLARE LENGTH: 7

INCLUDE: ../western_stdlib/stdlib.rules

; makes it easy to configure key
DEFINE: tonic = E
DEFINE: mode = sc_major

DEFINE: all_voices = [soprano, alto, tenor, bass]

; set key
REQUIRE: isDiatonic(tonic, sc_major, ANY_PITCH)
; TODO this will only allow v instead of V if minor

;;;;;;; VOICES

; absolute requirements for ranges
REQUIRE: pitches-of soprano at t >= C4 and pitches-of soprano at t <= G5
REQUIRE: pitches-of alto at t >= G3 and pitches-of alto at t <= C5
REQUIRE: pitches-of tenor at t >= C3 and pitches-of tenor at t <= G4
REQUIRE: pitches-of bass at t >= E2 and pitches-of bass at t <= G3

; spacing
REQUIRE: forall i in diads-of(alto, soprano), i <= iv_oct
REQUIRE: forall i in diads-of(tenor, alto), i <= iv_oct

;;;;;;; VOICE LEADING

; prefer small motion
PREFER: weight 3, contour-of v at t <= iv_maj3

; resolving tendency tones
REQUIRE: isScaleDegree(tonic, mode, 7, pitches-of v at t) => isScaleDegree(tonic, mode, 1, pitches-of v at (t + 1t))
PREFER: isScaleDegree(tonic, mode, 4, pitches-of v at t) => isScaleDegree(tonic, mode, 3, pitches-of v at (t + 1t))

; no augmented intervals
DISALLOW: contour-of v contains iv_aug4

; leaps larger than a P4 should change direction

REQUIRE: contour-of v at t > (5i up) => 
    (contour-of v at (t + 1t) < 0i up) and (contour-of v at (t + 1t) in iv_step) 
REQUIRE: contour-of v at t < (5i down) => 
    (contour-of v at (t + 1t) > 0i up) and (contour-of v at (t + 1t) in iv_step) 

; consecutive leaps should outline a triad
REQUIRE: (contour-of v at t > (2i up) and contour-of v at (t + 1t) > (2i up)) =>
    (exists [chord in ch_triads, 
                root in [pitches-of v at t, pitches-of v at (t + 1t), pitches-of v at (t + 2t)]] 
        where (isInChord(root, chord, pitches-of v at t) and
              isInChord(root, chord, pitches-of v at (t + 1t)) and
              isInChord(root, chord, pitches-of v at (t + 2t))))
REQUIRE: (contour-of v at t < (2i down) and contour-of v at (t + 1t)  < (2i down)) =>
    (exists [chord in ch_triads, 
                root in [pitches-of v at t, pitches-of v at (t + 1t), pitches-of v at (t + 2t)]] 
        where (isInChord(root, chord, pitches-of v at t) and
              isInChord(root, chord, pitches-of v at (t + 1t)) and
              isInChord(root, chord, pitches-of v at (t + 2t))))

; no more than two leaps in a row
REQUIRE: (contour-of v at t > 2i and contour-of v at (t + 1t) > 2i) =>
            contour-of v at (t + 2t) in [iv_min2, iv_maj2]


; bass begins and ends on tonic
REQUIRE: pitches-of bass at start is tonic
REQUIRE: pitches-of bass at end is tonic

;;;;;;;; MOTION 

; no parallel 5ths or octaves! avoid parallel 2nds and 7ths
REQUIRE: forall ival in [iv_oct, iv_p5],
    (v1 != v2) and (diads-of(v1, v2) at t is ival) and not isObliqueMotion(v1, v2, t)
        => (diads-of(v1, v2) at (t + 1t) is-not ival) 
            
PREFER: weight 3, forall ival in [iv_min2, iv_maj2, iv_min7, iv_maj7],
    (v1 != v2) and (diads-of(v1, v2) at t is ival) and not isObliqueMotion(v1, v2, t) => (diads-of(v1, v2) at (t + 1t) is-not ival)

; prefer contrary motion between soprano and the bass
PREFER: weight 1, isContraryMotion(soprano, bass, t)

;;;;;;;; HARMONY

; requires all voices to be in the same chord
REQUIRE: exists [root in note_classes, chord in ch_all] where 
    forall voice in all_voices, 
        isInChord(root, chord, pitches-of voice at ANY_TIME) 

; start and end on I 
REQUIRE: isInChord(tonic, ch_maj, pitches-of v at start)
REQUIRE: isInChord(tonic, ch_maj, pitches-of v at end)

;;;; these are for major
DEFINE: sd_1 = getScaleDegree(tonic, mode, 1)
DEFINE: sd_2 = getScaleDegree(tonic, mode, 2)
DEFINE: sd_3 = getScaleDegree(tonic, mode, 3)
DEFINE: sd_4 = getScaleDegree(tonic, mode, 4)
DEFINE: sd_5 = getScaleDegree(tonic, mode, 5)
DEFINE: sd_6 = getScaleDegree(tonic, mode, 6)
DEFINE: sd_7 = getScaleDegree(tonic, mode, 7)

; tonics: [I, iii, vi]
DEFINE: isTonic(t TimeStep) =
    (forall voice in all_voices, isInChord(sd_1, ch_maj, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_3, ch_min, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_6, ch_min, pitches-of voice at t))

; subdominants: [ii, ii7, IV]
DEFINE: isSubdominant(t TimeStep) =
    (forall voice in all_voices, isInChord(sd_2, ch_min, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_2, ch_min7, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_4, ch_maj, pitches-of voice at t))

; dominants: [V, V7, vii^o, vii^o7]
DEFINE: isDominant(t TimeStep) =
    (forall voice in all_voices, isInChord(sd_5, ch_maj, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_5, ch_dom7, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_7, ch_dim, pitches-of voice at t)) or
    (forall voice in all_voices, isInChord(sd_7, ch_dim7, pitches-of voice at t))

REQUIRE: isTonic(t) => 
                isSubdominant(t + 1t) or
                isDominant(t + 1t)

REQUIRE: isSubdominant(t) => 
                isTonic(t + 1t) or
                isDominant(t + 1t)

;REQUIRE: isDominant(t) => 
;                isTonic(t + 1t) or
;                isDominant(t + 1t)

REQUIRE: isDominant(t) => isTonic(t + 1t)

;;;;;;;; MISC PREFERENCES

; try to get more variety in the voices
;PREFER: weight 1, v1 != v2 => pitches-of v1 at t != pitches-of v2 at t

