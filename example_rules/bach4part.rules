DECLARE VOICES: soprano, alto, tenor, bass
DECLARE LENGTH: 5

INCLUDE: ../example_rules/stdlib/stdint.rules
INCLUDE: ../example_rules/stdlib/stdkey.rules
INCLUDE: ../example_rules/stdlib/stdpitch.rules
INCLUDE: ../example_rules/stdlib/stdchord.rules
INCLUDE: ../example_rules/stdlib/stdmotion.rules

; makes it easy to configure key
DEFINE: tonic = D
DEFINE: mode = sc_major

DEFINE: all_voices = [soprano, alto, tenor, bass]

; set key
REQUIRE: isDiatonic(tonic, sc_major, ANY_PITCH)
; TODO this will only allow v instead of V if minor

;;;;;;; VOICES

; absolute requirements for ranges
REQUIRE: pitches-of soprano at t >= C4 and pitches-of soprano at t <= G5
REQUIRE: pitches-of alto at t >= G3 and pitches-of alto at t <= C5
REQUIRE: pitches-of tenor at t >= C3 and pitches-of tenor at t <= G4
REQUIRE: pitches-of bass at t >= E2 and pitches-of bass at t <= G3

; spacing
REQUIRE: forall i in diads-of(alto, soprano), i < (12i up) and i > (12i down)
REQUIRE: forall i in diads-of(tenor, alto), i < (12i up) and i > (12i down)

;;;;;;; VOICE LEADING

; prefer stepwise motion
PREFER: weight 3, contour-of v1 at t is iv_min2 or contour-of v1 at t is iv_maj2

; no augmented intervals
DISALLOW: contour-of v contains iv_aug4
DISALLOW: contour-of v contains iv_aug2

; leading tone resolves
REQUIRE: isScaleDegree(tonic, mode, 7, pitches-of v at t) => isScaleDegree(tonic, mode, 1, pitches-of v at (t + 1t))
PREFER: weight 1, isScaleDegree(tonic, mode, 4, pitches-of v at t) => isScaleDegree(tonic, mode, 3, pitches-of v at (t + 1t))

; bass begins and ends on tonic
REQUIRE: pitches-of bass at start is tonic
REQUIRE: pitches-of bass at end is tonic

;;;;;;;; MOTION 

; no parallel 5ths or octaves!
REQUIRE: (v1 != v2) and (diads-of(v1, v2) at t is iv_p5) => (diads-of(v1, v2) at (t + 1t) is-not iv_p5)
REQUIRE: (v1 != v2) and (diads-of(v1, v2) at t is iv_oct) => (diads-of(v1, v2) at (t + 1t) is-not iv_oct)

;;;;;;;; HARMONY

; requires all voices to be in the same chord
REQUIRE: exists [root in note_classes, chord in ch_all] where 
    forall voice in all_voices, 
        isInChord(root, chord, pitches-of voice at ANY_TIME) 

; start and end on I 
REQUIRE: isInChord(tonic, ch_maj, pitches-of v at start)
REQUIRE: isInChord(tonic, ch_maj, pitches-of v at end)


;;;;;;;; MISC PREFERENCES

; try to get more variety in the voices
PREFER: weight 1, v1 != v2 => pitches-of v1 at t != pitches-of v2 at t
