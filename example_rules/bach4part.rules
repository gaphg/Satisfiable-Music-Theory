DECLARE VOICES: soprano, alto, tenor, bass
DECLARE LENGTH: 7

INCLUDE: ../western_stdlib/stdlib.rules

; makes it easy to configure key
DEFINE: tonic = C
DEFINE: mode = sc_major

DEFINE: all_voices = [soprano, alto, tenor, bass]

; set key
REQUIRE: isDiatonic(tonic, sc_major, ANY_PITCH)
; TODO this will only allow v instead of V if minor

;;;;;;; VOICES

; absolute requirements for ranges
REQUIRE: pitches-of soprano at t >= C4 and pitches-of soprano at t <= G5
REQUIRE: pitches-of alto at t >= G3 and pitches-of alto at t <= C5
REQUIRE: pitches-of tenor at t >= C3 and pitches-of tenor at t <= G4
REQUIRE: pitches-of bass at t >= E2 and pitches-of bass at t <= G3

; spacing
REQUIRE: forall i in diads-of(alto, soprano), i < (12i up) and i > (12i down)
REQUIRE: forall i in diads-of(tenor, alto), i < (12i up) and i > (12i down)

;;;;;;; VOICE LEADING
; prefer stepwise motion
PREFER: weight 3, contour-of v1 at t in [iv_min2, iv_maj2]

; resolving tendency tones
REQUIRE: isScaleDegree(tonic, mode, 7, pitches-of v at t) => isScaleDegree(tonic, mode, 1, pitches-of v at (t + 1t))
REQUIRE: isScaleDegree(tonic, mode, 4, pitches-of v at t) => isScaleDegree(tonic, mode, 3, pitches-of v at (t + 1t))

; no augmented intervals
DISALLOW: contour-of v contains iv_aug4

; leaps larger than a P4 should change direction
REQUIRE: contour-of v at t > (5i up) => 
    (exists step in [iv_min2, iv_maj2] 
        where (contour-of v at (t + 1t) = step)) and
            contour-of v at (t + 1t) < 0i up
REQUIRE: contour-of v at t < (5i down) => 
    (exists step in [iv_min2, iv_maj2] 
        where (contour-of v at (t + 1t) = step)) and
            contour-of v at (t + 1t) > 0i up

; consecutive leaps should outline a triad
REQUIRE: (contour-of v at t > (2i up) and contour-of v at (t + 1t) > (2i up)) =>
    (exists [chord in ch_triads, 
                root in [pitches-of v at t, pitches-of v at (t + 1t), pitches-of v at (t + 2t)]] 
        where (isInChord(root, chord, pitches-of v at t) and
              isInChord(root, chord, pitches-of v at (t + 1t)) and
              isInChord(root, chord, pitches-of v at (t + 2t))))
REQUIRE: (contour-of v at t < (2i down) and contour-of v at (t + 1t)  < (2i down)) =>
    (exists [chord in ch_triads, 
                root in [pitches-of v at t, pitches-of v at (t + 1t), pitches-of v at (t + 2t)]] 
        where (isInChord(root, chord, pitches-of v at t) and
              isInChord(root, chord, pitches-of v at (t + 1t)) and
              isInChord(root, chord, pitches-of v at (t + 2t))))

; no more than two leaps in a row
REQUIRE: (contour-of v at t > 2i and contour-of v at (t + 1t) > 2i) =>
            contour-of v at (t + 2t) in [iv_min2, iv_maj2]


; bass begins and ends on tonic
REQUIRE: pitches-of bass at start is tonic
REQUIRE: pitches-of bass at end is tonic

;;;;;;;; MOTION 

; no parallel 5ths or octaves! avoid parallel 2nds and 7ths
REQUIRE: forall ival in [iv_oct, iv_p5],
    (v1 != v2) and (diads-of(v1, v2) at t is ival) => (diads-of(v1, v2) at (t + 1t) is-not ival)
PREFER: weight 3, forall ival in [iv_min2, iv_maj2, iv_min7, iv_maj7],
    (v1 != v2) and (diads-of(v1, v2) at t is ival) => (diads-of(v1, v2) at (t + 1t) is-not ival)

; prefer contrary motion between soprano and the bass
PREFER: weight 1, isContraryMotion(soprano, bass, t)

;;;;;;;; HARMONY

; requires all voices to be in the same chord
REQUIRE: exists [root in note_classes, chord in ch_all] where 
    forall voice in all_voices, 
        isInChord(root, chord, pitches-of voice at ANY_TIME) 

; start and end on I 
REQUIRE: isInChord(tonic, ch_maj, pitches-of v at start)
REQUIRE: isInChord(tonic, ch_maj, pitches-of v at end)

;;;; these are for major
DEFINE: sd_1 = getScaleDegree(tonic, mode, 1)
DEFINE: sd_2 = getScaleDegree(tonic, mode, 2)
DEFINE: sd_3 = getScaleDegree(tonic, mode, 3)
DEFINE: sd_4 = getScaleDegree(tonic, mode, 4)
DEFINE: sd_5 = getScaleDegree(tonic, mode, 5)
DEFINE: sd_6 = getScaleDegree(tonic, mode, 6)
DEFINE: sd_7 = getScaleDegree(tonic, mode, 7)

; tonics: [I, iii, vi]
DEFINE: isTonic(p Pitch) = 
    isInChord(sd_1, ch_maj, p) or
    isInChord(sd_3, ch_min, p) or
    isInChord(sd_6, ch_min, p)

; subdominants: [ii, ii7, IV]
DEFINE: isSubdominant(p Pitch) = 
    isInChord(sd_2, ch_min, p) or 
    isInChord(sd_2, ch_min7, p) or 
    isInChord(sd_4, ch_maj, p)

; dominants: [V, V7, vii^o, vii^o7]
DEFINE: isDominant(p Pitch) =
    isInChord(sd_5, ch_maj, p) or 
    isInChord(sd_5, ch_dom7, p) or
    isInChord(sd_7, ch_dim, p) or 
    isInChord(sd_7, ch_dim7, p)

REQUIRE: isTonic(pitches-of v at t) => 
                isTonic(pitches-of v at (t + 1t)) or
                isSubdominant(pitches-of v at (t + 1t)) or
                isDominant(pitches-of v at (t + 1t))

REQUIRE: isSubdominant(pitches-of v at t) => 
                isTonic(pitches-of v at (t + 1t)) or
                isSubdominant(pitches-of v at (t + 1t)) or
                isDominant(pitches-of v at (t + 1t))

REQUIRE: isSubdominant(pitches-of v at t) => 
                isTonic(pitches-of v at (t + 1t)) or
                isDominant(pitches-of v at (t + 1t))

;;;;;;;; MISC PREFERENCES

; try to get more variety in the voices
PREFER: weight 1, v1 != v2 => pitches-of v1 at t != pitches-of v2 at t
