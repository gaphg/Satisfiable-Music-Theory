Program := ConfigurationStmt* DefinitionStmt* SpecificationStmt*

-- Configurations --
ConfigurationStmt := VoiceDecl | TimeUnitDecl | SongLengthDecl | KeyDecl
VoiceDecl := "DECLARE VOICES:" Id ("," Id)*
TimeUnitDecl := "DECLARE TIME UNIT TICKS:" Int
SongLengthDecl := "DECLARE MEASURES:" Int
KeyDecl := "DECLARE KEY:" PitchClass Mode

-- Definitions --
DefinitionStmt := ConstDef | FuncDef
ConstDef := "DEFINE:" Id "=" Expr
FuncDef := "DEFINE:" Id "(" Formal ("," Formal)* ")" "=" Expr
Formal := Id Type?

-- Specifications --
SpecificationStmt := RequireStmt | DisallowStmt | PreferStmt | AvoidStmt
RequireStmt := "REQUIRE:" Expr
DisallowStmt := "DISALLOW:" Expr
PreferStmt := "PREFER:" Expr
AvoidStmt := "AVOID:" Expr

-- The expressions! --
Expr := Literal
        | FuncCall
        | ListExpr
        | "pitches of" Expr
        | "contour of" Expr
        | "diads of" Expr
        | "interval between" of Expr
        | Expr "+" Expr
        | Expr "-" Expr
        | "not" Expr
        | Expr "and" Expr
        | Expr "or" Expr
        | Expr "=>" Expr
        | Expr "if" Expr
        | Expr "=" Expr
        | Expr "!=" Expr
        | Expr "<" Expr
        | Expr "<=" Expr
        | Expr ">" Expr
        | Expr ">=" Expr
        | Expr "at" Expr
        | Expr "contains" Expr
        | Expr "is" Expr            ; loose equality (mod octave)
        | Expr "is not" Expr
        | "flatten" Expr
        | "(" Expr ")"

Literal := Int "p"                      ; PitchLit
        | Int "i" Bool?                 ; IntervalLit
        | Int "t"                       ; TimeStepLit
        | Boolean                       ; BooleanLit
        | Int                           ; IntegerLit
        | Boolean "d"                   ; DirectionLit
        | Id                            ; Var

FuncCall := Id Id*
ListExpr := Expr+

-- Basic Types -- 
Type := "Voice" 
        | "Pitch" 
        | "Interval" 
        | "TimeStep" 
        | "Integer" 
        | "Boolean" 
        | "TimeSeries" "of" Type 
        | "List" "of" Type

-- Literals --
Id := <any alphanumeric string>
Int := <any nonnegative integer literal>
PitchClass := <a note name>                       ([a-gA-G][#b]?)
Mode := <"major" or "minor">                      (TODO : add other capitalization/modes?)
Bool := <"TRUE" or "FALSE">