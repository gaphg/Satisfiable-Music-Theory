-- Comments --
Single line comments begin with a ";"

Program := IncludeStmt* ConfigurationStmt* DefinitionStmt* SpecificationStmt*   ; TODO: this is not quite accurate -- a program has to have at least one of them

-- Includes --
IncludeStmt := "INCLUDE:" Filename

-- Configurations --
ConfigurationStmt := VoiceDecl | TimeUnitDecl | SongLengthDecl | KeyDecl
VoiceDecl := "DECLARE VOICES:" Id ("," Id)*
TimeUnitDecl := "DECLARE TIME UNIT TICKS:" Int            ;  TODO: unsupported feature
SongLengthDecl := "DECLARE LENGTH:" Int
KeyDecl := "DECLARE KEY:" PitchClass Mode                 ; TODO: unsupported feature

-- Definitions --
DefinitionStmt := ConstDef | FuncDef
ConstDef := "DEFINE:" Id "=" Expr
FuncDef := "DEFINE:" Id "(" FormalList ")" "=" Expr
FormalList := Formal ("," Formal)*
Formal := Id Type?

-- Specifications --
SpecificationStmt := RequireStmt | DisallowStmt | PreferStmt | AvoidStmt
RequireStmt := "REQUIRE:" Expr
DisallowStmt := "DISALLOW:" Expr
PreferStmt := "PREFER:" ("weight" Int ",")? Expr
AvoidStmt := "AVOID:" ("weight" Int ",")? Expr

-- The expressions! --
Expr := Literal
        | FuncCall
        | ListExpr
        | "pitches-of" Expr
        | "contour-of" Expr
        | "diads-of" "(" Expr "," Expr ")"
        | "interval-bt" "(" Expr "," Expr ")"
        | "flatten" Expr            ; put everything in same octave    
        | Expr "+" Expr
        | Expr "-" Expr
        | Expr "mod" Expr
        | "not" Expr
        | Expr "and" Expr
        | Expr "or" Expr
        | Expr "=>" Expr
        | Expr "<=>" Expr
        | "forall" "[" FormalList "]" "," Expr
        | "exists" "[" FormalList "]" "where" Expr
        | Expr "=" Expr
        | Expr "!=" Expr
        | Expr "is" Expr            ; loose equality (mod octave)
        | Expr "is-not" Expr
        | Expr "<" Expr
        | Expr "<=" Expr
        | Expr ">" Expr
        | Expr ">=" Expr
        | Expr "at" Expr            ; list indexing
        | Expr "contains" Expr
        | Expr "in" Expr
        | "(" Expr ")"

Literal := Int ("p"|"P")                ; PitchLit
        | Int ("i"|"I") DirectionLit?   ; IntervalLit
        | Int ("t"|"T")                 ; TimeStepLit
        | Boolean                       ; BooleanLit
        | Int                           ; IntegerLit
        | DirectionLit                  ; DirectionLit
        | Id                            ; Var

DirectionLit := "up"
        | "down"
FuncCall := Id "(" Literal ("," Literal)* ")"
ListExpr := "[" Expr ("," Expr)* "]"

-- Basic Types -- 
Type := "Voice" 
        | "Pitch" 
        | "Interval" 
        | "TimeStep" 
        | "Integer" 
        | "Boolean" 
        | "TimeSeries" "of" Type 
        | "List" "of" Type

-- Literals --
Id := <any alphanumeric string (can also have a # or _)>
Int := <any nonnegative integer literal>
Boolean := <"true" or "false">
Filename := <any non-whitespace string> "." <any non-whitespace string>