-- Comments --
Single line comments begin with a ";"

Program := IncludeStmt* ConfigurationStmt* DefinitionStmt* SpecificationStmt*
        TODO: this is not quite accurate -- as of right now there must be at least
        one of each

-- Includes --
IncludeStmt := "INCLUDE:" Filename

-- Configurations --
ConfigurationStmt := VoiceDecl | TimeUnitDecl | SongLengthDecl | KeyDecl
VoiceDecl := "DECLARE VOICES:" Id ("," Id)*
TimeUnitDecl := "DECLARE TIME UNIT TICKS:" Int
SongLengthDecl := "DECLARE MEASURES:" Int
KeyDecl := "DECLARE KEY:" PitchClass Mode
        TODO: KeyDecl does not work like this, it's just an int rn 

-- Definitions --
DefinitionStmt := ConstDef | FuncDef
ConstDef := "DEFINE:" Id "=" Expr
FuncDef := "DEFINE:" Id "(" FormalList ")" "=" Expr
FormalList := Formal ("," Formal)*
Formal := Id Type?

-- Specifications --
SpecificationStmt := RequireStmt | DisallowStmt | PreferStmt | AvoidStmt
RequireStmt := "REQUIRE:" Expr
DisallowStmt := "DISALLOW:" Expr
PreferStmt := "PREFER:" Expr
AvoidStmt := "AVOID:" Expr

-- The expressions! --
Expr := Literal
        | FuncCall
        | ListExpr
        | "pitches-of" Expr
        | "contour-of" Expr
        | "diads-of" "(" Expr "," Expr ")"
        | "interval-bt" "(" Expr "," Expr ")"
        | Expr "+" Expr
        | Expr "-" Expr
        | "not" Expr
        | Expr "and" Expr
        | Expr "or" Expr
        | Expr "=>" Expr
        | Expr "<=>" Expr
        | Expr "=" Expr
        | Expr "!=" Expr
        | Expr "<" Expr
        | Expr "<=" Expr
        | Expr ">" Expr
        | Expr ">=" Expr
        | Expr "at" Expr
        | Expr "contains" Expr
        | Expr "in" Expr
        | Expr "is" Expr            ; loose equality (mod octave)
        | Expr "is-not" Expr
        | "flatten" Expr            ; put everything in same octave    
        | "(" Expr ")"
        | "forall" "[" FormalList "]" "," Expr
        | "exists" "[" FormalList "]" "where" Expr

Literal := Int ("p"|"P")                ; PitchLit
        | Int ("i"|"I") Bool?           ; IntervalLit
        | Int ("t"|"T")                 ; TimeStepLit
        | Boolean                       ; BooleanLit
        | Int                           ; IntegerLit
        | Boolean ("d"|"D")             ; DirectionLit
        | Id                            ; Var

FuncCall := Id "(" Literal ("," Literal)* ")"
ListExpr := "[" Expr ("," Expr)* "]"

-- Basic Types -- 
Type := "Voice" 
        | "Pitch" 
        | "Interval" 
        | "TimeStep" 
        | "Integer" 
        | "Boolean" 
        | "TimeSeries" "of" Type 
        | "List" "of" Type

-- Literals --
Id := <any alphanumeric string>
Int := <any nonnegative integer literal>
PitchClass := <a note name>                       ([a-gA-G][#b]?)
Mode := <"major" or "minor">                      (TODO : add other capitalization/modes?)
Bool := <"TRUE" or "FALSE">
Filename := <any non-whitespace string> "." <any non-whitespace string>